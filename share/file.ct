module File
import Result as *
import List
import String
import Stream

def (open path mode) (Device.open :file {path, mode})
def (open! path mode) (unwrap! (Device.open :file {path, mode}))
def (close file) (Device.close file)
def (read file n) (Device.read file n)
def (read! file n) (unwrap! (Device.read file n))

def (stream path) do
  let buf_size = 1024

  def (stream-chunk-from n chunk file) do
    if n == #chunk do
      (stream-next-chunk file)
    else
      \-> do
        (chunk n) | (stream-chunk-from n + 1 chunk file)
      end
    end
  end

  def (stream-next-chunk file) do
    let chunk = (read! file buf_size)

    if chunk == nil do
      nil
    else
      stream-chunk-from 0 chunk file
    end
  end

  stream-next-chunk (open! path "r")
end

def (stream-lines path) do
  (Stream.chunk-while
    (stream path)
    \char -> char != String.newline)
end

def (list-dir path) do
  let dir = Device.open :directory {path}
  if not (ok? dir) do
    nil
  else
    let dir = (unwrap dir nil),
        items = Device.read dir nil
    Device.close dir
    unwrap items nil
  end
end

def (dirname path) do
  if path == nil do
    nil
  else
    let index = Enum.trim-end-to $/ path #path,
        index = Enum.trim-end $/ path index
    (head (Type.split-bin path index))
  end
end

def (basename path) do
  let index = Enum.last-index-of $/ path
  (tail (Type.split-bin path index))
end

def (abs-path path) do
  if #path == 0 do
    nil
  else
    let dir = Device.open :directory {path}

    if (ok? dir) do
      let path = unwrap (Device.get-param (unwrap! dir) :path) nil
      Device.close (tail dir)
      path
    else
      let dirpath = abs-path (dirname path)
      if dirpath do
        dirpath <> "/" <> (basename path)
      else
        nil
      end
    end
  end
end

def (join-path left right) do
  cond do
    #left == 0 ->
      right
    #right == 0 ->
      left
    left.(#left - 1) == $/ ->
      join-path (head (Type.split-bin left (#left - 1))) right
    right.0 == $/ ->
      join-path left (tail (Type.split-bin right 1))
    true ->
      left <> "/" <> right
  end
end

def (exists? path) do
  let path = abs-path path,
      dir = dirname path

  List.any? \item -> path == (join-path dir item.0) (list-dir dir)
end
