stmts         → stmts stmt NL
stmts         → stmts stmt $
stmts         → nl
stmt          → let_stmt
stmt          → def_stmt
stmt          → import_stmt
stmt          → call
opt_stmt      → stmt
opt_stmt      → ε
let_stmt      → "let" nl assigns
assigns       → assigns "," nl assign
assigns       → assign
assign        → ID "=" call
def_stmt      → "def" "(" params ")" do_block
params        → params ID
params        → ID
import_stmt   → "import" STR
import_stmt   → "import" STR "as" ID
call          → call arg
call          → arg
ml_call       → ml_call nl arg
ml_call       → arg
arg           → lambda
lambda        → "(" ")" "->" nl logic
lambda        → group "->" nl logic
lambda        → ID "->" nl logic
lambda        → logic
logic         → logic "and" nl equals
logic         → logic "or" nl equals
logic         → equals
equals        → compare "==" nl compare
equals        → compare "!=" nl compare
equals        → compare
compare       → sum "<" nl sum
compare       → sum "<=" nl sum
compare       → sum ">" nl sum
compare       → sum ">=" nl sum
compare       → sum
sum           → sum "+" nl product
sum           → sum "-" nl product
sum           → product
product       → product "*" nl block
product       → product "/" nl block
product       → block
block         → do_block
block         → if_block
block         → cond_block
block         → primary
do_block      → "do" stmts opt_stmt "end"
if_block      → "if" arg "do" stmts opt_stmt "else" stmts opt_stmt "end"
if_block      → "if" arg do_block
cond_block    → "cond" "do" clauses "end"
clauses       → clauses clause
clauses       → NL
clause        → call "::" nl call NL
primary       → NUM
primary       → ID
primary       → STR
primary       → literal
primary       → symbol
primary       → group
primary       → list
primary       → tuple
primary       → dict
primary       → access
access        → access "." ID
access        → ID "." ID
literal       → "true"
literal       → "false"
literal       → "nil"
symbol        → ":" ID
group         → "(" ml_call ")"
list          → "[" items update "]"
items         → items arg opt_comma nl
items         → nl
tuple         → "#[" items "]"
dict          → "{" entries update "}"
entries       → entries entry opt_comma nl
entries       → nl
entry         → ID ":" nl arg
update        → "|" nl arg
update        → ε
nl            → NL
nl            → $
nl            → ε
opt_comma     → ","
opt_comma     → ε
